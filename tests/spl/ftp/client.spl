include "../../../lib/socket.spl";
include "../../../lib/file.spl";
include "../../../lib/console.spl";
include "../array/string.spl";
include "response.spl";
include "doWeUpload.spl";

procedure connectTo(ip: Array<Byte>, port: Array<Byte>)
  returns (fd: Int)
  requires (ip == null || byte_array(ip)) &*& byte_array(port)
  ensures (ip == null || byte_array(ip)) &*& byte_array(port)
  ensures ip == old(ip) && port == old(port) && port.length == old(port.length)
  ensures ip == null || ip.length == old(ip.length)
  ensures fd >= -1
{
  fd := create_socket(PF_INET, SOCK_STREAM, IPPROTO_TCP);
  if (fd == -1) {
    return -1;
  }
  var addr := get_address4(ip, port); //null should return localhost
  if (addr == null) {
    return -1;
  }

  if (connect4(fd, addr)) {
    free(addr);
    return fd;
  } else {
    free(addr);
    return -1;
  }
}

procedure setupDataConnection(cmdFd: Int, port: Array<Byte>)
  returns (connectedDataFD: Int)
  requires cmdFd >= 0
  requires port.length == 5;
  requires byte_array(port)
  ensures byte_array(port)
  ensures port == old(port) && port.length == old(port.length)
  ensures connectedDataFD >= -1
{
  var dataAddr := get_address4(null, port);
  if (dataAddr == null){
    return -1;
  }
  var dataFD := create_socket(PF_INET, SOCK_STREAM, IPPROTO_TCP);
  if (dataFD == -1){
    free(dataAddr);
    return -1;
  }
  var bound := bind4(dataFD, dataAddr);
  if (!bound) {
    free(dataAddr);
    return -1;
  }
  var datalistening := glisten(dataFD, 10);
  //we don't need a big backlog here, as the only connection should be to the server
  if (!datalistening){
    free(dataAddr);
    return -1;
  }
  //we tell the server what port we are setting the data connection on
  if ((port.length < 0) || (port.length > (65535 - 6))){ //this is to cover for the max length of an array, as set in byte_array.spl  We subtract 6 to account for PORT
    free(dataAddr);
    return -1;
  }
  var portMsg := new Array<Byte>(6 + port.length);
  portMsg := "PORT ";
  var copied := gstrcat(port, portMsg);
  var sent := tcp_send(cmdFd, portMsg, portMsg.length);
  free(portMsg);
  if (sent != portMsg.length) {
    free(dataAddr);
    return -1;
  }

  //now we wait for the server to connect to us on the data connection
  connectedDataFD := accept4(dataFD, dataAddr);
  free(dataAddr);
  var closeData := gclose(dataFD);
  return connectedDataFD;
}

procedure authenticate(cmdFd: Int)
  returns (success: Bool)
  requires cmdFd >= 0
{
  var userMsg := new Array<Byte>(12);
  userMsg := "USER potato"; //it's anonymous login, so you can put whatever here.  It's not checked
  var sent := tcp_send(cmdFd, userMsg, 12);
  free(userMsg);
  //receive confirmation by the server
  var okMsg := new Array<Byte>(4);
  var ok := tcp_recv(cmdFd, okMsg);
  //valid responses are 200, 230, 234, 300, 331
  var acceptables := new Array<Int>(5);
  acceptables[0] := 200;
  acceptables[1] := 230;
  acceptables[2] := 234;
  acceptables[3] := 300;
  acceptables[4] := 331;
  var checked := checkServerResponse(okMsg, acceptables);
  free(okMsg);
  free(acceptables);
  if (!checked) {
    return false;
  }
      /*  var ack := atoi(okMsg);
  if ((ack != 200) && (ack != 230) && (ack != 234) && (ack != 300) && (ack != 331)) {
    free(okMsg);
    return false;
    }*/
  // free(okMsg);

  var passMsg := new Array<Byte>(15);
  passMsg := "PASS anonymous";
  sent := tcp_send(cmdFd, passMsg, 15);
  free(passMsg);
  //receive confirmation by the server.  Acceptable responses include 200, 230, 202, 234
  okMsg := new Array<Byte>(4);
  ok := tcp_recv(cmdFd, okMsg);
  var acceptThese := new Array<Int>(4);
  acceptThese[0] := 200;
  acceptThese[1] := 202;
  acceptThese[2] := 230;
  acceptThese[3] := 234;
  checked := checkServerResponse(okMsg, acceptThese);
  free(okMsg);
  free(acceptThese);
  if (!checked) {
    return false;
  }
/*  ack := atoi(okMsg);
  if ((ack != 200) && (ack != 202) && (ack != 230) && (ack != 234)) {
    free(okMsg);
    return false;
  }
  free(okMsg);*/
  return true;
}

procedure askFilename()
  returns (fn: Array<Byte>)
  ensures fn == null || byte_array(fn)
{
  var filename := new Array<Byte>(100); //we decide max filename length is 100

  var text := new Array<Byte>(24);
  text := "input the file name:";
  var putted := gputs(text);
  free(text);
  if (!putted) {
    free(filename);
    return null;
  }
  var numChars := ggets(filename);

  if (numChars >= 100 || numChars <= 1){ //switched to 1 to catch the newline character at the end of the line
    free(filename);
    return null;
  }
  filename[numChars -1] := int2byte(0);
  return filename;

}

procedure uploadFile(cmdFd: Int, dataFD: Int, filename: Array<Byte>)
  returns (success: Bool)
  requires cmdFd >= 0 && dataFD >= 0
  requires byte_array(filename)
  ensures byte_array(filename)
{
  var size := fileSize(filename);
  if ((size < 0) || (size > 65535)){
    return false;
  }
  var opened := gopen(filename, O_CREAT | O_RDONLY);
  if (opened < 0){
    return false;
  }
  var buffer := new Array<Byte>(size);
  var read := gread(opened, buffer);
  if (read < 0){
    free(buffer);
    return false;
  }
  if ((filename.length < 0) || (filename.length > (65535 - 6))) {
    free(buffer);
    return false;
  }
  var commandSize := filename.length + 6; //7??
  var sendMsg := new Array<Byte>(commandSize);
  sendMsg := "STOR ";
  var copied := gstrcat(filename, sendMsg);
  var sent := tcp_send(cmdFd, sendMsg, commandSize);
  free(sendMsg);
  //Get confirmation from the server
  var okMsg := new Array<Byte>(4);
  var ok := tcp_recv(cmdFd, okMsg);

  var acceptThese := new Array<Int>(2);
  acceptThese[0] := 200;
  acceptThese[1] := 150;
  var checked := checkServerResponse(okMsg, acceptThese);
  free(okMsg);
  free(acceptThese);
  if (!checked) {
    free(buffer);
    return false;
  }

  /*  var ack := atoi(okMsg);
  if ((ack != 200) && (ack != 150)) { //note that it might actually return something else that starts with two, but ends in other things (200, 150)
    free(okMsg);
    free(buffer);
    return false;
  }
  free(okMsg);
  */
  var sentData := tcp_send(dataFD, buffer, size);
  //get confirmation from the server (200, 226, 250)
  okMsg := new Array<Byte>(4);
  ok := tcp_recv(cmdFd, okMsg);

  var pleaseAccept := new Array<Int>(3);
  pleaseAccept[0] := 200;
  pleaseAccept[1] := 226;
  pleaseAccept[2] := 250;
  checked := checkServerResponse(okMsg, pleaseAccept);
  free(okMsg);
  free(pleaseAccept);
  free(buffer);
  if (!checked) {
    return false;
  }
  /*
  ack := atoi(okMsg);
  if ((ack != 200) && (ack != 226) && (ack != 250)) {
    free(okMsg);
    free(buffer);
    return false;
  }
  free(okMsg);
  free(buffer);
  */
  var close := gclose(opened);
  if (sentData == size) {
    return true;
  } else {
    return false;
  }
}

procedure downloadFile(cmdFd: Int, dataFD: Int, filename: Array<Byte>)
  returns (success: Bool)
  requires cmdFd >= 0 && dataFD >= 0
  requires byte_array(filename)
  ensures byte_array(filename)
{
  if ((filename.length <= 0) || (filename.length > (65535-6))) { //maybe we should include zero as well.  65535-6 comes from byte_array.spl's max length.  We subtract 6 because we add 6 later on.  
    return false;
  }
  var cmdSize := 6 + filename.length;
  var sizeMsg := new Array<Byte>(cmdSize);
  sizeMsg := "SIZE ";
  var copied := gstrcat(filename, sizeMsg);
  var sent := tcp_send(cmdFd, sizeMsg, cmdSize);
  //We get confirmation from the server when it sends us the size back.  
  free(sizeMsg); 
  var sizeBuff := new Array<Byte>(128); //this could probably be smaller
  var recvData := tcp_recv(cmdFd, sizeBuff); //response gets sent on command buffer

  var pleaseAccept := new Array<Int>(1);
  pleaseAccept[0] := 213;
  var checked := checkServerResponse(sizeBuff, pleaseAccept);
  free(pleaseAccept);
  if (!checked) {
    free(sizeBuff);
    return false;
  }
  /*
  var sizeCode := atoi(sizeBuff);
  if (sizeCode != 213){
    free(sizeBuff);
    return false;
  } 
  */
  //this ensures we receive a valid size response
  var size := atoiFrom(sizeBuff, 4);
  free(sizeBuff);
  if ((size <= 0) || (size > 65535)) { //maybe we should include zero as well.  65535-6 comes from byte_array.spl's max length.  We subtract 6 because we add 6 later on.  
    return false;
  }
  var buffer := new Array<Byte>(size);
  var recvMsg := new Array<Byte>(cmdSize);
  recvMsg := "RETR ";
  copied := gstrcat(filename, recvMsg); 
  sent := tcp_send(cmdFd, recvMsg, cmdSize);
  free(recvMsg);

  var okMsg := new Array<Byte>(4);
  var ok := tcp_recv(cmdFd, okMsg);

  var acceptThese := new Array<Int>(2);
  acceptThese[0] := 200;
  acceptThese[1] := 150;
  checked := checkServerResponse(okMsg, acceptThese);
  free(okMsg);
  free(acceptThese);
  if (!checked) {
    free(buffer);
    return false;
  }

  recvData := tcp_recv(dataFD, buffer);
  //get response
  okMsg := new Array<Byte>(4);
  ok := tcp_recv(cmdFd, okMsg);

  pleaseAccept := new Array<Int>(3);
  pleaseAccept[0] := 200;
  pleaseAccept[1] := 226;
  pleaseAccept[2] := 250;
  checked := checkServerResponse(okMsg, pleaseAccept);
  free(okMsg);
  free(pleaseAccept);

  if (!checked) {
    free(buffer);
    return false;
  }

  if (recvData < 0){
    free(buffer);
    return false;
  }
  var saveFD := gopen(filename, O_CREAT | O_WRONLY | O_TRUNC); //here we save the file under the same name as it is stored on the server.  We use O_TRUNC to wipe the file before we overwrite it  
  if (saveFD < 0){
    free(buffer);
    return false;
  }
  var written := gwrite(saveFD, buffer);
  free(buffer);
  if (written < 0){
    return false;
  }
  var close := gclose(saveFD);
  return true;
}

procedure client(upload: Bool) 
  returns (res: Int)
  requires emp;
  ensures  emp;
{
//if store is 1, then we store a file STOR
// else we download it RETR
//client can say PASS for now "anonymous"
//Client can say USER for now "dummy"

  var port := new Array<Byte>(5);
  port := "4444";
  var fd := connectTo(null, port);
  if (fd == -1) {
    free(port);
    var closed := gclose(fd);
    return 2;
  }

  //we set up the data connection
  port := "4440";
  var connectedDataFD := setupDataConnection(fd, port);
  free(port);
  if (connectedDataFD == -1) {
    var closed := gclose(fd);
    closed := gclose(connectedDataFD);
    return 3;
  }
  //now we can receive a bunch of data

  //Note that at this point:
  //-fd is the command file descriptor
  //-connectedDataFD is the data file descriptor.

  var authenticated := authenticate(fd);
  if (!authenticated) {
    var closed := gclose(fd);
    closed := gclose(connectedDataFD);
    return 4;
  }

  var filename := askFilename();
  
  if (filename == null) {
    var closed := gclose(fd);
    closed := gclose(connectedDataFD);
    return 5;
  }

  var success := false;
  if (upload){
    //we store the file
    success := uploadFile(fd, connectedDataFD, filename);
  } else {
    //we retrieve the file
    success := downloadFile(fd, connectedDataFD, filename);
  }
  free(filename);
  if (!success) {
    var closed := gclose(fd);
    closed := gclose(connectedDataFd);
    return 6;
  }
  var closeConn := gclose(connectedDataFD);
  if (closeConn < 0){
    return 22;
  }
  var quitMsg := new Array<Byte>(5);
  quitMsg := "QUIT";
  var sent := tcp_send(fd, quitMsg, 5);
  free(quitMsg);
  //we can check for the correct quitting response or not.  
  var closeFD := gclose(fd);
  if (closeFD < 0){
    return 21;
  }
  return 0;
}

procedure Main(args: Array<Byte>)
  returns (res: Int)
  requires byte_array(args)
  ensures  byte_array(args)
{
  var upload := doWeUpload();
  if (upload == 1){
    res := client(true);
  } else if (upload == 0){
    res := client(false);
  } else {
    res := -1;
  }
  return res;
}
